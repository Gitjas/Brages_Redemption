/* set variables for detection of mod-added content */
IF	%BGT_VAR%
	InParty(Myself)
	!See([ENEMY])
	CombatCounter(0)
	Global("#L_SoDExitModded","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("#L_BG2ExitModded","LOCALS",1)
		SetGlobal("#L_SoDExitModded","LOCALS",1)
END

/* Transitions. SoD AND direct BGII transition. Brage will leave. */

IF	%BGT_VAR%
	InParty(Myself)
	IfValidForPartyDialog(Myself)
	OR(3)
		Global("#L_StartCaelarAttack","GLOBAL",1) 
		Global("#L_StartCaelarAttack","GLOBAL",2) 
		Global("#L_StartCaelarAttack","GLOBAL",3) 
	Global("C#Brage_TransitionsLeaveSoD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brage_TransitionsLeaveSoD","GLOBAL",1)
END

IF
	InParty(Myself)
	IfValidForPartyDialog(Myself)
	Global("C#Brage_TransitionsLeaveSoD","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

IF	%BGT_VAR%
	InParty(Myself)
	IfValidForPartyDialog(Myself)
	Global("#L_StartBG2","GLOBAL",1) 
	Global("C#Brage_TransitionsLeaveBGII","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brage_TransitionsLeaveBGII","GLOBAL",1)
END


IF
	InParty(Myself)
	IfValidForPartyDialog(Myself)
	Global("C#Brage_TransitionsLeaveBGII","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* After timer run out, player had time to snatch some items */
IF
	IfValidForPartyDialog(Myself)
	Global("C#Brage_TransitionsLeave","GLOBAL",1)
	GlobalTimerExpired("C#Brage_TransitionsLeaveTmr","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("C#Brage_TransitionsLeave","GLOBAL",2)
END

IF
	IfValidForPartyDialog(Myself)
	Global("C#Brage_TransitionsLeave","GLOBAL",2)
THEN
	RESPONSE #100
		Dialog(Player1)
END
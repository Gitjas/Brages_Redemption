
ACTION_IF GAME_IS ~bgee~ AND NOT GAME_INCLUDES ~sod~ THEN BEGIN

<<<<<<<< ...inlined/last_bg1_dialogue.baf
/* BG:EE without SoD: initiate dialogue in Undercity */

IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	AreaCheck("%Undercity%")
	Range("Door0125",20)
	!See("Tamoko")
	Global("C#BE_LastBG1Dialogue","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#BE_LastBG1Dialogue","GLOBAL",1)
END

>>>>>>>>
END //BG:EE


ACTION_IF GAME_INCLUDES ~sod~ THEN BEGIN

<<<<<<<< ...inlined/last_bg1_dialogue.baf
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	!Range([NEUTRAL],10)
	CombatCounter(0)
	Dead("Sarevok")
	!AreaCheck("%Undercity_TempleofBhaal%") /* will trigger in bd0120, but this way it will be compatible with a "delay SoD transition after Sarevok's death" tweak (note to Arthas: no, I'm not really planning on writing one. ;) )*/
	AreaCheck("bd0120")
	Global("C#BE_LastBG1Dialogue","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#BE_LastBG1Dialogue","GLOBAL",1)
END

/* switch to SoD override scrit after last dialogue fired. I will just use the same variable for this. */
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	CombatCounter(0)
	AreaCheck("bd0120")
	Global("C#BE_LastBG1Dialogue","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#BE_LastBG1Dialogue","GLOBAL",3)
		ChangeAIScript("c#brageS",OVERRIDE) //switches to SoD script 
END


>>>>>>>>
END //SoD

ACTION_IF GAME_IS ~bgt~ THEN BEGIN

<<<<<<<< ...inlined/last_bg1_dialogue.baf
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	Dead("Sarevok")
	!AreaCheck("%Undercity_TempleofBhaal%") //after stepping out of the temple
	Global("C#BE_LastBG1Dialogue","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#BE_LastBG1Dialogue","GLOBAL",1)
END

>>>>>>>>
END //BGT


<<<<<<<< ...inlined/last_bg1_dialogue_2.baf

/* initiate */
IF 
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	Global("C#BE_LastBG1Dialogue","GLOBAL",1) 
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
		
END

>>>>>>>>
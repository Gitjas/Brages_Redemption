/* brage_bd1000.baf
Brage moves to first war camp at Coast Way Crossing, bd1000.are */

/* 1. It's a new soD game, or Brage wasn't spawned yet in SoD because the player went straight for the crusade */
IF
  !Dead("C#Brage")
  !InPartyAllowDead("C#Brage")
  Global("C#BE_SpawnBrage","GLOBAL",0) //true for a new SoD game or if Brage wasn't spawned in SoD before
  Global("C#Brage_MoveCamp","MYAREA",0) //so it happens only once
THEN
  RESPONSE #100
    SetGlobal("C#Brage_MoveCamp","MYAREA",1)
    SetGlobal("C#BE_SpawnBrage","GLOBAL",11)
    CreateCreature("C#BRAGES",[760.3735],NW)  
    ActionOverride("C#Brage",MakeGlobalOverride())
    ChangeSpecifics("C#Brage",ALLIES)
    ActionOverride("C#Brage",ChangeAIScript("C#Brages",OVERRIDE)) //Brage's SoD override script
    ActionOverride("C#Brage",ChangeAIScript("C#Brages",OVERRIDE))
    ActionOverride("C#Brage",ChangeAIScript("bdasc3",CLASS))
    ActionOverride("C#Brage",ChangeAIScript("BDSHOUT",RACE))
    ActionOverride("C#Brage",ChangeAIScript("BDFIGH01",GENERAL))
    ActionOverride("C#Brage",ChangeAIScript("",DEFAULT))
    ActionOverride("C#Brage",SetDialog("C#BrageS") 
    Continue() 
END

/* 2+3: Brage was in party before (e.g. as a BG:EE NPC or in SoD beginning) OR he was spawned in SoD before */
IF
  Global("C#Brage_MoveCamp","MYAREA",0)
  !Dead("C#Brage")
  !InPartyAllowDead("C#Brage")
  OR(2) //One of the 2 following conditions needs to be true
	BeenInParty("C#Brage") //this is true if Brage was in party before
	GlobalGT("C#BE_SpawnBrage","GLOBAL",8)
THEN
  RESPONSE #100
    ActionOverride("C#Brage",SetDialogue("C#BrageP"))
    SetGlobal("C#Brage_MoveCamp","MYAREA",1)
    SetGlobal("C#BE_SpawnBrage","GLOBAL",11)
    MoveGlobal("bd1000","C#Brage",[760.3735]) 
    ActionOverride("C#Brage",Face(NW))
    ReallyForceSpellDeadRES("bdrejuve","C#Brage")
    ChangeEnemyAlly("C#Brage",NEUTRAL)
    ChangeSpecifics("C#Brage",ALLIES)
    ActionOverride("C#Brage",SetGlobal("bd_joined","locals",0))
    ActionOverride("C#Brage",SetGlobal("bd_retreat","locals",0))
    ActionOverride("C#Brage",SaveObjectLocation("LOCALS","bd_default_loc",Myself))
    ActionOverride("C#Brage",ChangeAIScript("C#Brages",OVERRIDE))
    ActionOverride("C#Brage",ChangeAIScript("bdasc3",CLASS))
    ActionOverride("C#Brage",ChangeAIScript("BDSHOUT",RACE))
    ActionOverride("C#Brage",ChangeAIScript("BDFIGH01",GENERAL))
    ActionOverride("C#Brage",ChangeAIScript("",DEFAULT))
    Continue()
END

/* bd1000_commenting.baf: 
Reaction to Caelar */

IF
	Global("bd_plot","global",170) //after talking to Bence, this will be at Global("bd_plot","global",175) 
	Global("C#Brage_bd_mdd016b","bd1000",0)
	!GlobalTimerNotExpired("bd_mdd016b_timer","bd1000") //timer to space the NPC comments
	IsValidForPartyDialog("C#Brage")  
	TriggerOverride("C#Brage",Detect(Player1))
THEN
	RESPONSE #100
		SetGlobal("C#Brage_bd_mdd016b","bd1000",1)
		SetGlobalTimer("bd_mdd016b_timer","bd1000",ONE_ROUND)
		DisplayStringHead("C#Brage",@1003)  
    Continue() 
END
/* brage_bd4300_commenting.baf */
/* 1 Hephernaan discovered the group inside the castle */
IF
	Global("bd_plot","global",370)
	!GlobalTimerNotExpired("bd_mdd905a_ot_timer","bd4300")  
	Global("C#Brage_bd_ot","bd4300",0)  
	IfValidForPartyDialog("C#Brage")  
THEN
	RESPONSE #100
		SetGlobalTimer("bd_mdd905a_ot_timer","bd4300",THREE_MINUTES)  
		SetGlobal("C#Brage_bd_ot","bd4300",1)  
		DisplayStringHead("C#Brage",@1009)  
		Continue()  
END

/* 2 Portal is opened after Hephernaan's scheme */
IF
	Global("bd_plot","global",495)
	Global("C#Brage_SoDPortalComment","bd4300",0) 
	IfValidForPartyDialog("C#Brage")  
THEN
	RESPONSE #100
		SetGlobal("C#Brage_SoDPortalComment","bd4300",1)
		DisplayStringHead("C#Brage",@1010)  
		SmallWait(8)
		Continue()
END


/* 3 PC killed the crusaders at the portal */
IF
	Global("C#Brage_bd_MDD892a_ot","bd4300",0)  
	Global("bd_poison_dsc_supplies","global",0)
	Dead("bdbelben")  // Belben
	CombatCounter(0)
	IfValidForPartyDialog("C#Brage")  
THEN
	RESPONSE #100
		SetGlobal("C#Brage_bd_MDD892a_ot","bd4300",1)  
		SetGlobalTimer("bd_MDD892a_ot_timer","bd4300",ONE_MINUTE) //timer will be checked for Corwin 
		DisplayStringHead("C#Brage",@1011)   
		Continue() 
END

/* 4 Comment after first wave of demons if PC lingers */
IF
	GlobalLT("bd_plot","global",499)
	Global("bd_mdd1290d_ot","bd4300",1)  
	Global("C#Brage_bd_mdd1290d_ot","bd4300",0)  
	IfValidForPartyDialog("C#Brage")  
THEN
	RESPONSE #100
		DisplayStringHead("C#Brage",@1012)  
		SetGlobal("C#Brage_bd_mdd1290d_ot","bd4300",1) 
		SmallWait(8)   
		Continue()
END

/* 5 Comment after return from Avernus: portal is closed */
IF
	Global("bd_plot","global",586)
	!GlobalTimerNotExpired("bd_mdd1341a_ot_timer","bd4300")  
	Global("C#Brage_bd_ot2","bd4300",0)  
	IfValidForPartyDialog("C#Brage")  
THEN
	RESPONSE #100
		SetGlobalTimer("bd_mdd1341a_ot_timer","bd4300",2)  // Dragonspear Castle Basement
		SetGlobal("C#Brage_bd_ot2","bd4300",1)  
		DisplayStringHead("C#Brage",@1013)  
		SmallWait(8)
		Continue()
END

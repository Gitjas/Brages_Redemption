/* BGII script */



IF
	!InParty(Myself)
	Global("TempleShout0901","GLOBAL",1)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		Enemy()
END

/* Lilarcor... */

/* Lilarcor is no longer in Brage's equipped slot - disable timer */
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	!HasItemEquiped("SW2H14",Myself)
	GlobalLT("C#BE_Lilarcor","GLOBAL",3)
	!Global("C#BE_LilarcorEquipped","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#BE_LilarcorEquipped","GLOBAL",2)
END

/* Lilracor is in Brage's equipped slot - start timer */
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	HasItemEquiped("SW2H14",Myself)
	GlobalLT("C#BE_Lilarcor","GLOBAL",3)
	!Global("C#BE_LilarcorEquipped","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#BE_LilarcorEquippedTimer","GLOBAL",3600)
		SetGlobal("C#BE_LilarcorEquipped","GLOBAL",1)
END


/* first Lilarcor reaction */
/* activate */
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
//	PartyHasItem("SW2H14")
	HasItemEquiped("SW2H14",Myself)
//	HasItem(Myself,"SW2H14")
	Global("C#BE_Lilarcor","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#BE_Lilarcor","GLOBAL",1)
END
/* initiate */
IF 
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	Global("C#BE_Lilarcor","GLOBAL",1) 
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
		
END

/* second Lilarcor reaction */
/* activate */
IF
	Global("C#BE_LilarcorEquipped","GLOBAL",1)
	RealGlobalTimerExpired("C#BE_LilarcorEquippedTimer","GLOBAL")
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
//	PartyHasItem("SW2H14")
	HasItemEquiped("SW2H14",Myself)
//	HasItem(Myself,"SW2H14")
THEN
	RESPONSE #100
		SetGlobal("C#BE_LilarcorEquipped","GLOBAL",3)
END
/* initiate */
IF 
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	Global("C#BE_LilarcorEquipped","GLOBAL",3) 
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
		
END

/* Brage -Nalia banter: toggle dialogue order depending on whether her father is already dead */


IF
	Dead("Torgal")
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	InParty("nalia")
THEN
	RESPONSE #100
		SetGlobal("C#BE_NaliaCondolences","GLOBAL",1)
END
IF
	InMyArea(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(6)
		See(Player1)
		See(Player2)
		See(Player3)
		See(Player4)
		See(Player5)
		See(Player6)
	Global("C#Brage_SoDQuest","GLOBAL",10)
	CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogNoSet(Myself)
END

IF
	Global("C#Brage_SoDQuest","GLOBAL",10)
	!See(Player1)
	!See(Player2)
	!See(Player3)
	!See(Player4)
	!See(Player5)
	!See(Player6)
	CombatCounter(0)
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

IF
	GlobalGT("C#BE_SoDQuestWaitTimer","MYAREA",0)	
	GlobalLT("C#BE_SoDQuestWaitTimer","MYAREA",4)
	RealGlobalTimerNotExpired("C#BE_SoDQuestTimer","GLOBAL")
	!Range(Player1,20)
	!Range(Player2,20)
	!Range(Player3,20)
	!Range(Player4,20)
	!Range(Player5,20)
	!Range(Player6,20)
THEN
	RESPONSE #100
		SetGlobal("C#BE_SoDQuestWaitTimer","MYAREA",5)
END

IF
	InMyArea(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(6)
		Range(Player1,20)
		Range(Player2,20)
		Range(Player3,20)
		Range(Player4,20)
		Range(Player5,20)
		Range(Player6,20)
	GlobalLT("C#Brage_SoDQuest","GLOBAL",10)
	RealGlobalTimerExpired("C#BE_SoDQuestTimer","GLOBAL")
	CombatCounter(0)
	OR(3)
		Global("C#BE_SoDQuestWaitTimer","MYAREA",1)
		Global("C#BE_SoDQuestWaitTimer","MYAREA",2)
		Global("C#BE_SoDQuestWaitTimer","MYAREA",3)
THEN
	RESPONSE #100
		StartDialogNoSet(Myself)
END

IF
	AreaCheck("bd3000") 
	Global("C#Brage_SoDQuest","GLOBAL",20)
THEN
	RESPONSE #100
		EscapeAreaDestroy(5)
END

IF
	AreaCheck("bd7300") 
	GlobalGT("C#Brage_SoDQuest","GLOBAL",10)
THEN
	RESPONSE #100
		EscapeAreaDestroy(5)
END

IF
	GlobalGT("bd_plot","global",494) //SoD is over
THEN
	RESPONSE #100
		DestroySelf()
END